
buildscript {
    ext.kotlin_version = '0.11.91.1'
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'

apply plugin: 'maven'
apply plugin: 'signing'

test {
    useTestNG()
    testLogging.showStandardStreams = true
    testLogging {
        events "passed", "skipped", "failed"
    }
    exclude 'com/github/salomonbrys/kodein/test/TestPackage$test$**'
}

group = "com.github.salomonbrys.kodein"
archivesBaseName = "kodein"
version = '1.1'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

//task copyFiles << {
//    copy{
//        from configurations.compile, configurations.testCompile
//        into "$buildDir/copy"
//    }
//}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    testCompile 'org.testng:testng:6.8.21'
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Kodein'
                packaging 'jar'
                artifactId 'kodein'
                description 'Kotlin Dependency Injection'
                url 'https://github.com/SalomonBrys/Kodein'

                licenses {
                    license {
                        name 'MIT'
                        url 'http://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'salomonbrys'
                        name 'Salomon BRYS'
                        email 'salomon.brys@gmail.com'
                    }
                }

                scm {
                    url 'https://github.com/SalomonBrys/Kodein'
                    connection 'scm:git:https://github.com/SalomonBrys/Kodein.git'
                    developerConnection 'scm:git:git@github.com:SalomonBrys/Kodein.git'
                    tag 'HEAD'
                }

                issueManagement {
                    system 'GitHub Issues'
                    url 'https://github.com/SalomonBrys/Kodein/issues'
                }
            }
        }
    }
}
