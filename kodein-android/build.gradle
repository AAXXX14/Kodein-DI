apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'

    defaultConfig {
        minSdkVersion 15
    }

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !travisBuild
    }
}

task androidJavadocJar(type: Jar) {
    classifier = 'javadoc'
    from dokka
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

artifacts {
    archives androidSourcesJar, androidJavadocJar
}

signing {
    required { gradle.taskGraph.hasTask(tasks.uploadArchives) }
    sign configurations.archives
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
    processConfigurations = ['compile', 'provided']
    includes = ['dokka-module.md', 'dokka-package.md']

    linkMapping {
        dir = "src/main/java"
        url = "https://github.com/SalomonBrys/Kodein/blob/master/kodein-android/src/main/kotlin"
        suffix = "#L"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile project(':kodein-core')

    compile 'com.android.support:appcompat-v7:25.3.1'

    testCompile "junit:junit:$junit_version"
}

if (hasProperty('ossrhUsername') && hasProperty('ossrhPassword')) {
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'Kodein-Android'
                    packaging 'jar'
                    artifactId 'kodein-android'
                    description 'Kodein extension for Android'
                    url 'https://github.com/SalomonBrys/Kodein'

                    licenses {
                        license {
                            name 'MIT'
                            url 'http://opensource.org/licenses/MIT'
                        }
                    }

                    developers {
                        developer {
                            id 'salomonbrys'
                            name 'Salomon BRYS'
                            email 'salomon.brys@gmail.com'
                        }
                    }

                    scm {
                        url 'https://github.com/SalomonBrys/Kodein'
                        connection 'scm:git:https://github.com/SalomonBrys/Kodein.git'
                        developerConnection 'scm:git:git@github.com:SalomonBrys/Kodein.git'
                        tag 'HEAD'
                    }

                    issueManagement {
                        system 'GitHub Issues'
                        url 'https://github.com/SalomonBrys/Kodein/issues'
                    }
                }
            }
        }
    }
}
