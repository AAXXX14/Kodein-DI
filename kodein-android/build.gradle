
archivesBaseName = "kodein-android"

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask(tasks.uploadArchives) }
    sign configurations.archives
}

configurations {
  provided
}

sourceSets.main.compileClasspath += [configurations.provided]

idea {
    module {
        scopes.PROVIDED.plus += [configurations.provided]
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile project(':kodein')

    provided 'com.google.android:android:4.0.1.2'

    provided 'com.google.android:support-v4:r7'

    testCompile 'junit:junit:4.12'
}

if (hasProperty('ossrhUsername') && hasProperty('ossrhPassword')) {
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'Kodein-Android'
                    packaging 'jar'
                    artifactId 'kodein-android'
                    description 'Kotlin Dependency Injection for Android'
                    url 'https://github.com/SalomonBrys/Kodein'

                    licenses {
                        license {
                            name 'MIT'
                            url 'http://opensource.org/licenses/MIT'
                        }
                    }

                    developers {
                        developer {
                            id 'salomonbrys'
                            name 'Salomon BRYS'
                            email 'salomon.brys@gmail.com'
                        }
                    }

                    scm {
                        url 'https://github.com/SalomonBrys/Kodein'
                        connection 'scm:git:https://github.com/SalomonBrys/Kodein.git'
                        developerConnection 'scm:git:git@github.com:SalomonBrys/Kodein.git'
                        tag 'HEAD'
                    }

                    issueManagement {
                        system 'GitHub Issues'
                        url 'https://github.com/SalomonBrys/Kodein/issues'
                    }
                }
            }
        }
    }
}
